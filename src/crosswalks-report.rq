#+ summary: Cell types per organ per annotation tool
PREFIX ccf: <http://purl.org/ccf/>
PREFIX CCF: <https://purl.humanatlas.io/graph/ccf>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?Organ 
  (SUM(IF(?tool = 'azimuth', 1, 0)) as ?Azimuth)
  (SUM(IF(?tool = 'celltypist', 1, 0)) as ?CellTypist)
  (SUM(IF(?tool = 'popv', 1, 0)) as ?popV)
  (SAMPLE(?_hasReferenceOrgan) as ?hasReferenceOrgan)
WHERE {
  {
    SELECT DISTINCT ?tool ?organ_id ?cell_id
    WHERE {
      ?annotation a ccf:AnnotationItem ;
          ccf:tool ?tool ;
          ccf:organ_id ?organ_id ;
          ccf:organ_level ?organ_level ;
          ccf:cell_id ?cell_id ;
      .
    }
  }

  {
    SELECT ?organ_id (SAMPLE(?_Organ) as ?Organ)
    WHERE {
      ?annotation a ccf:AnnotationItem ;
        ccf:organ_id ?organ_id .
      OPTIONAL {
        ?organ_id ccf:ccf_pref_label ?OrganPrefLabel .
      }
      OPTIONAL {
        ?organ_id rdfs:label ?OrganRdfsLabel .
      }

      BIND(COALESCE(?OrganPrefLabel, ?OrganRdfsLabel, IF(?organ_id = <http://purl.obolibrary.org/obo/UBERON_0001134>, 'skeletal muscle tissue', ?organ_id)) as ?_Organ)
    }
    GROUP BY ?organ_id
  }

  OPTIONAL {
    {
      ?refOrgan ccf:representation_of ?organ_id .
    }
    UNION
    {
      ?refOrgan ccf:representation_of [
        ccf:ccf_part_of ?organ_id
      ] .
    }
  }

  BIND(IF(BOUND(?refOrgan), 'x', '') as ?_hasReferenceOrgan) 
}
GROUP BY ?Organ
ORDER BY ?Organ
